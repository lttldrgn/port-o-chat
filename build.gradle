apply plugin: 'application'
apply plugin: 'com.google.protobuf'
version = '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "com.lttldrgn.portochat.client.ClientLauncher"

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
  }
}

repositories {
    mavenCentral()
}
dependencies {
    compile 'com.google.protobuf:protobuf-java:3.5.1'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/"
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
}

task fullJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'Gradle Jar File',
            'Implementation-Version': version,
            'Main-Class': mainClassName
    }
    baseName = 'Port-O-Chat'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    } {
//        exclude ""
    }
    with jar
}

task deploy(type: Copy, dependsOn:'build') {
    from 'src/logging.properties'
    from 'scripts'
    from 'build/libs/'
    into 'build/deploy/'
}

task debug(type: JavaExec) {
    debug = true
    classpath = sourceSets.main.runtimeClasspath
    main = mainClassName
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.lttldrgn.portochat.server.ServerLauncher'
}

task debugServer(type: JavaExec) {
    debug = true
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.lttldrgn.portochat.server.ServerLauncher'
}

allprojects {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}